#!/bin/env zsh 

typeset -A COLORS=(
    info  4,2
    error 1,5
    help 3,2
    none 2,6
)

mktemp_file() {
    local filename
    filename=$(mktemp) || {
        print "tempfile create error"
        exit $?
    }
    trap "rm -f $filename" EXIT SIGINT SIGTERM
    print $filename
}

report() {
    [[ -n $o_level ]] && print -P "%S%B%F{${o_colors%,*}}$o_level:%s%f%b"
    (($#)) && print -P "%F{${o_colors#*,}}$(print -l ${o_indent:+'\t'}${^@})%f"
}

main() {
    local o_indent o_colors o_level
    zparseopts -E -D -F -A zopts -- -level: -indent -colors: || exit $? 

    (($+zopts[--indent])) && o_indent=1
    (($+zopts[--colors])) && o_colors=$zopts[--colors]
    (($+zopts[--level])) && o_level=$zopts[--level]

    [[ -z $o_colors ]] && o_colors=${${COLORS[${o_level:-none}]:-$COLORS[none]}} 
    
    report "$@"
}

main "$@"
