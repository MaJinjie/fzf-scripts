#!/bin/env zsh

# 1. 能够解析常用的参数
# 2. 接受目录或文件
# 3. 尽可能地解释用户传入的参数(很完美，几乎完全避免了和文件名或模式冲突)

REPORT_BIN=tools-report

report_info() { $REPORT_BIN --indent --level "info" -- "$@" > /dev/tty }
report_warn() { $REPORT_BIN --indent --level "warn" -- "$@" > /dev/tty }
report_help() { $REPORT_BIN --level "help" -- "$@" > /dev/tty; exit 0 }
report_error() { $REPORT_BIN --indent --level "error" -- "$@" 2> /dev/tty; exit 1 }

help() {
    local help_msg='usage: ff [OPTIONS] [DIRECTORIES or Files]

    OPTIONS:
        -g glob-based search
        -p full-path
        -t char set, file types dfxlspebc (-t t...)
        -T string, changed after time ( 1min 1h 1d(default) 2weeks "2018-10-27 10:00:00" 2018-10-27)
        -d int, max-depth
        -H bool, --hidden
        -I bool, --no-ignore
        -e extensions
        -E exclude glob pattern
        -o select + -O
        -q Cancel the first n matching file names (Optional default 1)

        --help 
        --window full none
        --split Explain the parameters passed in by the user as much as possible
        --args pass -> fd

    KEYBINDINGS:
        ctrl-s horizontal direction splitw
        ctrl-v vertical direction splitw
        alt-e subshell editor
        alt-enter open dirname file
        enter current open file
    '
    report_help "${(@f)help_msg}"
}

__split() {
    ((DEBUG)) && set -x
    local item idx
    local match class context flags
    for item in "$@"; do 
        { [[ -e $item ]] && ((o_q-- < 1)) } && {
            [[ -d $item ]] && Directories+=$item || Files+=$item
            continue
        }
        ((o_split)) && [[ $item =~ '^([[:alpha:]]([[:punct:]]).*\2)+[[:alpha:]]*$'  ]] && {
            flags=( ${(s//)item##*[[:punct:]]} )
            while [[ $item =~ '^[[:alpha:]]([[:punct:]]).*?\1' ]]; do
                class=$MATCH[1] context=$MATCH[3,-2] match=$MATCH 
                case $class in 
                    f) 
                        while (($#context)); do 
                            if [[ $context =~ ^h ]]; then
                                ((idx=$o_args[(I)--hidden])) && unset "o_args[idx]" || o_args+=--hidden
                            elif [[ $context =~ ^i ]]; then
                                ((idx=$o_args[(I)--no-ignore])) && unset "o_args[idx]" || o_args+=--no-ignore
                            elif [[ $context =~ ^g ]]; then
                                ((idx=$o_args[(I)--glob])) && unset "o_args[idx]" || o_args+=--glob
                            elif [[ $context =~ ^a ]]; then
                                ((idx=$o_args[(I)--full-path])) && unset "o_args[idx]" || o_args+=--full-path
                            elif [[ $context =~ ^[dfxlebcsp] ]]; then
                                o_types+=$MATCH
                            elif [[ $context =~ ^[[:digit:]]+ ]]; then
                                unset "o_args[${o_args[(i)--max-depth*]}]"
                                ((MATCH)) && o_args+="--max-depth=$MATCH"
                            else
                                report_error "class:$class" "$context" "内容错误"
                            fi
                            context=${context#$MATCH}
                        done
                        ;;
                    t)
                        local after before 
                        ((flags[(I)r])) && {after=before; before=after;} || {after=after; before=before;}
                        [[ $context =~ ^[^,]* && -n $MATCH ]] && o_args+="--changed-$after=$MATCH"
                        context=${context#$MATCH}
                        [[ $context =~ [^,]*$ && -n $MATCH ]] && o_args+="--changed-$before=$MATCH"
                        ;;
                    e)
                        ((flags[(I)r])) && o_excludes+=( $(print -- \*.${^${(s/,/)context}}) ) || o_extensions+=( ${(s/,/)context} )
                        ;;
                    x|E)
                        o_excludes+=( $(print -- ${^${(s/,/)context}}) )                        ;;
                    *)
                        report_error "class:$class" "类别错误"
                        ;;
                esac
                item=${item#$match}
            done
            continue
        }
        [[ -z $Pattern ]] || report_error "$item error, pattern is exists"
        Pattern=$item
    done
    ((DEBUG)) && set +x
}

__handle_result() {
    local trigger_key files
    trigger_key=${"${(@f)Results}"[1]}
    files=(${"${(@f)Results}"[2,-1]})
    
    if [[ "alt-s" == $trigger_key ]]; then
        tmux splitw -bv ${EDITOR} $files
    elif [[ "alt-v" == $trigger_key ]]; then
        tmux splitw -bh ${EDITOR} $files    
    elif [[ "alt-enter" == $trigger_key ]]; then
        $EDITOR ${files[-1]:h} > /dev/tty < /dev/tty
    else
        $EDITOR $files > /dev/tty < /dev/tty
    fi
}

main() {
    local o_q o_extensions=() o_types=() o_excludes=() o_window o_split o_output o_args=()
    zparseopts -D -E -F -A zopts -- q:: g p t: T: d: H I u e: E: -window: -output -help -split -args: || exit 1
    setopt extended_glob
    setopt rematchpcre

    (($+zopts[-g])) && o_args+="--glob"
    (($+zopts[-p])) && o_args+="--full-path"
    (($+zopts[-u])) && o_args+="-u"
    (($+zopts[-H])) && o_args+="--hidden"
    (($+zopts[-I])) && o_args+="--no-ignore"
    (($+zopts[-d])) && o_args+="--max-depth=$zopts[-d]"
    (($+zopts[-t])) && o_types+=( ${(s//)zopts[-t]} )
    (($+zopts[-T])) && o_args+="--change-after=$zopts[-T]"
    (($+zopts[-e])) && o_extensions+=( ${(s/,/)zopts[-e]} )
    (($+zopts[-E])) && o_excludes+=( ${(s/,/)zopts[-E]} )
    (($+zopts[-q])) && o_q=${zopts[-q]:-1}
    
    (($+zopts[--split]))  && o_split=1
    (($+zopts[--output])) && o_output=1
    (($+zopts[--window])) && o_window=$zopts[--window]
    (($+zopts[--args]))   && o_args+=(${=zopts[--args]})
    (($+zopts[--help]))   && help

    local Directories=() Files=() Pattern Results
    local fbin cmd fopts exitcode  
    
    (($#)) && __split "$@"
    
    [[ $o_window == full ]] && fbin=(fzf-tmux -p100%,100%) || fbin=(fzf-tmux $FZF_TMUX_OPTS)
    [[ $o_window == none ]] && fbin=

    cmd="command fd --color=always --follow $o_args"

    fopts="
    $FZF_DEFAULT_OPTS
    --prompt 'Files> '
    --exit-0
    --scheme path --exact --tiebreak \"length,end,chunk,index\"
    --delimiter / --nth -1,-2
    --header-first --header=\"keybindings:: C-s, C-v, C-o, A-e, A-enter\"
    --expect \"alt-s,alt-v,alt-o,alt-enter\"
    --bind=\"alt-e:execute(${EDITOR} {+} > /dev/tty < /dev/tty)\"
    --bind=\"alt-s:accept,alt-v:accept,alt-enter:accept,enter:accept\"
    $FZF_CUSTOM_OPTS
    "
    
    Results=$( 
    { 
        ((DEBUG)) && set -x
        (($#Files)) && lscolors <<<${(F)Files}
        (($#Directories || !$#Files)) && eval $cmd --type=${^o_types:-f} --extension=${^o_extensions} \"--exclude=${^o_excludes}\" \"${Pattern:-\^}\" $Directories
        ((DEBUG)) && set +x
    } | FZF_DEFAULT_OPTS=$fopts ${fbin:-fzf})
    exitcode=$?

    if ((exitcode == 1)); then report_warn  "没有搜索到任何相关的文件";
    elif ((exitcode == 2)); then report_error;
    elif ((exitcode == 130)); then :;
    elif ((o_output)); then print $Results;
    else __handle_result;fi

    return $exitcode
}
main "$@"
