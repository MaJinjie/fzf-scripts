#!/bin/env zsh

typeset -A LOG=(
    info  42
    error 15
    help 32
)
FD_BIN=fzf-fd
REPORT_BIN=tools-report
ERROR_FILE=/tmp/error-$$; trap "rm -f $ERROR_FILE" EXIT SIGINT SIGTERM
valid_commands=(rm cp mv)


report_info() { $REPORT_BIN --indent --level "info" -- "$@" > /dev/tty }
report_warn() { $REPORT_BIN --indent --level "warn" -- "$@" > /dev/tty }
report_help() { $REPORT_BIN --level "help" -- "$@" > /dev/tty; exit 0 }
report_error() { $REPORT_BIN --indent --level "error" -- "$@" 2> /dev/tty; exit 1 }

__rm__() {
    local flag_args cmd fopts results
    zparseopts -E -D -F -A zopts -- v r f i -help -cmd: 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}"

    (($+zopts[--help])) && report_help "args: -v -f -r -i"
    (($+zopts[--cmd])) && cmd=$zopts[--cmd]
    (($+zopts[-v])) && flag_args+="-v "
    (($+zopts[-f])) && flag_args+="-f "
    (($+zopts[-i])) && flag_args+="-i "
    (($+zopts[-r])) && flag_args+="-r "

    cmd="command ${cmd:-rm} $flag_args"
    fopts="--prompt \"To-Rm > \" "

    # -- 后的选项和参数传递给fd脚本
    (((rmidx=$@[(i)--]) < $#)) && set -- "${@[1,rmidx-1]}" "${@[rmidx+1,-1]}"
    results=$(FZF_CUSTOM_OPTS=$fopts $FD_BIN --output --split -d1 "$@")
    
    [[ -n $results ]] && { eval $cmd ${(f)results} 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" }
}
__cp__() {
    local flag_args cmd fopts results
    zparseopts -E -D -F -A zopts -- v r f i b u t: -help -cmd: 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" 
    (($+zopts[--help])) && report_help "args: -v -f -i -r -b -u -t"
    (($+zopts[--cmd])) && cmd=$zopts[--cmd]
    (($+zopts[-v])) && flag_args+="-v "
    (($+zopts[-f])) && flag_args+="-f "
    (($+zopts[-i])) && flag_args+="-i "
    (($+zopts[-r])) && flag_args+="-r "
    (($+zopts[-b])) && flag_args+="-b "
    (($+zopts[-u])) && flag_args+="-u "
    ((! $+zopts[-t])) && [[ -d $1 ]] && { zopts[-t]=$1; shift }
    (($+zopts[-t])) && flag_args+="-t $zopts[-t] " || report_error "Need a specified directory"

    cmd="command ${cmd:-cp} $flag_args"
    fopts="--prompt \"To-Cp > \" "

    (((rmidx=$@[(i)--]) < $#)) && set -- "${@[1,rmidx-1]}" "${@[rmidx+1,-1]}"
    results=$(FZF_CUSTOM_OPTS=$fopts $FD_BIN --output --split -d1 -E $zopts[-t] "$@")
    
    [[ ! -d $zopts[-t] ]] && { mkdir -p $zopts[-t] 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" }
    [[ -n $results ]] && { eval $cmd ${(f)results} 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" }
}

__mv__() {
    local flag_args cmd fopts results
    zparseopts -E -D -F -A zopts -- v n f i b u t: -help -cmd: 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" 
    (($+zopts[--help])) && report_help "args: -v -f -i -n -b -u -t"
    (($+zopts[--cmd])) && cmd=$zopts[--cmd]
    (($+zopts[-v])) && flag_args+="-v "
    (($+zopts[-f])) && flag_args+="-f "
    (($+zopts[-i])) && flag_args+="-i "
    (($+zopts[-n])) && flag_args+="-n "
    (($+zopts[-b])) && flag_args+="-b "
    (($+zopts[-u])) && flag_args+="-u "
    ((! $+zopts[-t])) && [[ -d $1 ]] && { zopts[-t]=$1; shift }
    (($+zopts[-t])) && flag_args+="-t $zopts[-t] " || {  report_error "Need a specified directory" }

    cmd="command ${cmd:-mv} $flag_args"
    fopts="--prompt \"To-Mv > \" "

    (((rmidx=$@[(i)--]) < $#)) && set -- "${@[1,rmidx-1]}" "${@[rmidx+1,-1]}"
    results=$(FZF_CUSTOM_OPTS=$fopts $FD_BIN --output --split -d1 -E $zopts[-t] "$@")
    
    [[ ! -d $zopts[-t] ]] && { mkdir -p $zopts[-t] 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}"  }
    [[ -n $results ]] && { eval $cmd ${(f)results} 2> $ERROR_FILE || report_error "${(f@)$(<$ERROR_FILE)}" } 
}

(($# && ${valid_commands[(I)$1]})) || report_error "command is empty or error"
mode=$1
shift
__${mode}__ "$@"
